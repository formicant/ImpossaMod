    MODULE AY


; Copied to RAM page 1 #C000

score:  ; #c000
.part0:
        dh 80 2C 30 2D 30 2C 30 31 30 2C 30 2D 30 2C 30 25 30 FF
.part1:
        dh 7F 30 FF
.part2:
        dh 80 91 1C 30 92 20 30 1F 30 22 30 20 30 93 23 30 22 30 94 27 30 90 FF
.part3:
        dh 81 01 18 01 18 FF
.part4:
        dh 81 01 18 01 18 01 18 01 18 01 18 01 18 01 0C 01 0C 01 0C 01 0C 01 18 01 18 01 18 01 18 01 18 01 18 01 0C 01 0C 01 0C 01 0C FF
.part5:
        dh 82 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 2A 06 12 06 36 06 1E 06 12 06 12 06 12 06 12 0C 12 0C 12 06 12 06 FF
.part6:
        dh 81 91 23 12 23 12 23 12 92 27 12 27 0C 27 0C 29 12 29 12 29 12 22 12 22 0C 22 0C 90 FF
.part7:
        dh 81 0D 06 0D 06 0D 0C 0D 0C 0D 06 0D 0C 0D 0C 0D 06 08 0C 0B 0C FF
.part8:
        dh 81 10 06 10 06 10 0C 10 0C 10 06 10 06 0B 06 0B 06 0B 0C 0B 0C 0B 06 0B 06 FF
.part9:
        dh 81 0D 06 19 0C 0D 06 19 0C 0D 06 19 0C 0D 06 19 0C 0B 0C 08 0C FF
.part10:
        dh 81 0D 06 0D 06 0D 0C 0D 12 0D 0C 0D 06 0D 0C 0D 0C 19 06 19 06 FF
.part11:
        dh 81 10 06 10 06 10 0C 10 0C 10 06 10 06 0B 06 0B 06 0B 0C 0B 0C 0B 06 0B 06 FF
.part12:
        dh 81 0D 06 0D 06 19 0C 0D 0C 19 06 0D 0C 0D 06 19 0C 0D 0C 19 0C 09 06 09 06 15 0C 09 0C 15 06 0B 0C 0B 06 17 0C 0B 0C 17 0C 0D 06 0D 06 19 0C 0D 0C 19 06 0D 0C 0D 06 19 0C 0D 0C 19 0C 10 06 10 06 1C 0C 10 0C 1C 06 0B 0C 0B 06 17 0C 0B 0C 17 0C FF
.part13:
        dh 81 0D 06 0D 0C 0D 06 0D 0C 0D 06 0D 12 0B 0C 10 0C 0B 0C 09 06 09 0C 09 0C 09 0C 09 06 0B 0C 08 0C 0B 0C 0D 0C FF
.part14:
        dh 83 2A 0C 2A 0C 1E 0C 2A 0C 2A 06 2A 0C 2A 06 1E 0C 1E 0C 2A 0C 2A 0C 1E 12 2A 0C 2A 06 2A 06 2A 06 1E 06 12 06 06 06 06 06 06 0C 06 0C 2A 0C 1E 0C 1E 0C 2A 0C 1E 0C 2A 0C 1E 0C 1E 0C 2A 0C 2A 06 2A 06 2A 06 36 06 36 06 2A 06 1E 06 12 06 06 06 06 06 FF
.part15:
        dh 84 19 0C 20 06 20 06 1F 0C 20 0C 25 0C 24 0C 25 0C 27 0C 19 0C 21 06 21 06 20 0C 21 0C 23 0C 21 0C 20 0C 1C 0C 19 0C 20 06 20 06 1F 0C 20 0C 25 0C 24 0C 25 0C 27 0C 28 0C 2A 06 2C 06 2D 0C 2C 06 2A 06 2C 0C 2A 0C 28 0C 27 0C FF
.part16:
        dh 84 25 0C 20 0C 25 0C 2C 0C 28 18 27 18 25 0C 20 0C 25 0C 2C 0C 28 0C 27 0C 25 0C 23 0C 25 0C 20 0C 25 0C 2C 0C 28 18 27 0C 23 0C 21 30 20 30 FF
.part17:
        dh 84 25 78 28 06 27 06 28 06 27 06 28 0C 2A 0C 28 0C 23 0C 25 78 28 06 27 06 28 06 27 06 28 0C 2A 0C 28 0C 29 0C FF
.part18:
        dh 80 2A 78 2D 06 2C 06 2D 06 2C 06 2D 0C 2F 0C 2D 0C 28 0C 2A 60 85 2A 60 FF
.part19:
        dh 84 14 0C 17 0C 16 0C 17 0C 14 0C 23 0C 22 0C 20 0C 1B 0C 1E 0C 1D 0C 1E 0C 1B 0C 2A 0C 29 0C 27 0C 1E 0C 21 0C 20 0C 21 0C 1E 0C 2D 0C 2C 0C 2A 0C 34 0C 33 0C 31 0C 28 0C 27 0C 25 0C 1C 0C 1B 0C FF
.part20:
        dh 84 14 0C 17 0C 16 0C 17 0C 14 0C 23 0C 22 0C 20 0C 1B 0C 1E 0C 1D 0C 1E 0C 1B 0C 2A 0C 29 0C 27 0C 1E 0C 21 0C 20 0C 21 0C 1E 0C 2D 0C 2C 0C 2A 0C 22 0C 25 0C 24 0C 25 0C 22 0C 25 0C 28 0C 2B 0C FF
.part21:
        dh 84 94 2C 0C 2C 18 2D 18 2C 18 2A 0C 2C 60 90 86 01 03 FF
.part22:
        dh 7F 60 7F 60 7F 60 7F 60 FF
.part23:
        dh 84 17 60 85 17 60 7F 60 7F 60 FF
.part24:
.part25:
        dh 82 12 60 12 60 12 60 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 12 06 FF
.part26:
        dh 81 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 06 0D 06 0D 0C 0D 06 0D 06 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 0C 0D 06 0D 06 0D 0C 0D 0C 0D 06 0D 0C 0D 06 0D 06 0D 06 0D 0C 0D 06 0D 06 FF
.part27:
        dh 81 14 06 14 06 14 0C 14 12 14 0C 14 06 14 0C 14 0C 14 06 14 06 0F 06 0F 06 0F 0C 0F 0C 0F 06 0F 0C 0F 0C 0F 06 0F 0C 0F 06 0F 06 12 06 12 06 12 0C 12 12 12 0C 12 06 12 0C 12 0C 12 06 12 06 0D 06 0D 06 0D 0C 0D 12 0D 0C 0D 06 0D 0C 0D 0C 0D 06 0D 06 FF
.part28:
        dh 81 14 06 14 06 14 0C 14 12 14 0C 14 06 14 0C 14 0C 14 06 14 06 0F 06 0F 06 0F 0C 0F 0C 0F 06 0F 0C 0F 0C 0F 06 0F 0C 0F 06 0F 06 12 06 12 06 12 0C 12 12 12 0C 12 06 12 0C 12 0C 12 06 12 06 16 06 0A 06 0A 0C 0A 12 0A 0C 0A 06 0A 0C 0A 0C 0A 06 0A 06 FF
.part29:
        dh 81 08 0C 08 18 09 18 08 18 06 0C 08 60 FF


; used in playMenuMusic
musicInitAddrs: ; #C4CB
.start: dw startA, startB, startC
.stop:  dw stopCh, stopCh, stopCh

startA:
        dh 88 03 04 1A 83 07
        dh 08 09 FE
        dh F9 09 FE 00 09 FE F9 09
        dh FE 00 83 0A 0B 0C 82 0D
        dh 0C 83 0A 0B 0C 82 0D 0C
        dh 0C FE 05 0C FE 00 1B 1C
        dh 1D FF
startB:
        dh 00 02 FE 05 02 8E
        dh 05 FF
startC:
        dh 16 16 19 17 82 06
        dh 0E 0F 10 16 0E 0F 10 10
        dh 11 12 13 14 15 FF
stopCh:
        db 1, -1

scorePartAddrsLow:
        db low(score.part0),   low(score.part1),   low(score.part2)
        db low(score.part3),   low(score.part4),   low(score.part5)
        db low(score.part6),   low(score.part7),   low(score.part8)
        db low(score.part9),   low(score.part10),  low(score.part11)
        db low(score.part12),  low(score.part13),  low(score.part14)
        db low(score.part15),  low(score.part16),  low(score.part17)
        db low(score.part18),  low(score.part19),  low(score.part20)
        db low(score.part21),  low(score.part22),  low(score.part23)
        db low(score.part24),  low(score.part25),  low(score.part26)
        db low(score.part27),  low(score.part28),  low(score.part29)
scorePartAddrsHigh:
        db high(score.part0),  high(score.part1),  high(score.part2)
        db high(score.part3),  high(score.part4),  high(score.part5)
        db high(score.part6),  high(score.part7),  high(score.part8)
        db high(score.part9),  high(score.part10), high(score.part11)
        db high(score.part12), high(score.part13), high(score.part14)
        db high(score.part15), high(score.part16), high(score.part17)
        db high(score.part18), high(score.part19), high(score.part20)
        db high(score.part21), high(score.part22), high(score.part23)
        db high(score.part24), high(score.part25), high(score.part26)
        db high(score.part27), high(score.part28), high(score.part29)

p_c55c: ; #C55C
        db (1<<7)|(1<<3)|2, (4<<3)|1, (9<<3)|1, 0, 0, 0, 0, 0
        db (1<<7)|(1<<3)|2, (3<<3)|1, (8<<3)|1, 0, 0, 0, 0, 0
        db (1<<7)|(1<<3)|2, (5<<3)|1, (9<<3)|1, 0, 0, 0, 0, 0
        db (1<<7)|(1<<3)|2, (4<<3)|1, (7<<3)|1, 0, 0, 0, 0, 0
        db (1<<7)|(4<<3)|1, (6<<3)|1,        0, 0, 0, 0, 0, 0

; Instruments used in the music (8 × 10 bytes)
instruments: ; #C584
        ;     env  dec sus rel envP  ?  viP viS flag
.i0:    Instr #60, -1, 48, -1, #30, #10, 6,  1, %001
.i1:    Instr #7F, -3,  0, -2, #37, #00, 0,  0, %001
.i2:    Instr #7F, -6,  0, -1, #28, #00, 1, 24, %010
.i3:    Instr #7F, -4,  0, -1, #28, #00, 1, 24, %010
.i4:    Instr #7F, -2, 50, -1, #3E, #20, 3,  2, %001
.i5:    Instr #7F, -1, 50, -1, #40, #00, 0,  1, %001
.i6:    Instr #7F, -2,  0, -1, #00, #20, 3,  2, %001
.i7:    Instr #60, -1,  0, -1, #30, #10, 6,  1, %001


; Game sound effects (15 × 13 bytes)
aySounds:  ; #c5d4
        ;        env  dec sus  rel envP  ?  viP  viS  flag   period dur
        Effect { #7F, -23, 1,  -1, #7F, #00, 0,  163, %001 }, 1498,  1  ; [0]
        Effect { #1B,  -1, 1,  -1, #50, #00, 0,    1, %001 },   47,  1  ; [1]
        Effect { #7F, -23, 1,  -1, #7F, #00, 0, -215, %001 },  846,  1  ; [2]
        Effect { #08, -14, 1,  -7, #6B, #FF, 0,    0, %101 }, 3900,  2  ; [3] damage enemy
        Effect { #0E, -14, 1,  -7, #29, #FF, 0,    0, %010 },   88,  1  ; [4]
        Effect { #7F,  -4, 1,  -1, #44, #00, 0,  -20, %001 }, 2154,  1  ; [5]
        Effect { #7F,  -3, 1,  -1, #60, #00, 0, -256, %010 },  240,  1  ; [6] kill enemy
        Effect { #0C,   0, 0,   0, #71, #00, 0,  156, %101 }, 4027, 15  ; [7]
        Effect { #0A,  -6, 1, -10, #00, #00, 0,   -1, %101 },   35, 28  ; [8]
        Effect { #14, -20, 1,  -1, #7F, #01, 0,    0, %010 },  124, 10  ; [9]
        Effect { #7F,  -3, 1,  -1, #7F, #04, 0,  -32, %001 }, 2413,  1  ; [10]
        Effect { #7F,  -6, 1,  -1, #7F, #00, 0,  -14, %001 },  637,  1  ; [11]
        Effect { #7F, -10, 1,  -1, #7F, #00, 0,    0, %001 }, 3460,  1  ; [12] energy loss
        Effect { #7F,  -7, 1,  -1, #7F, #00, 0,    0, %001 },  200,  1  ; [13]
        Effect { #7F,  -3, 8, -35, #7E, #00, 0, -803, %001 }, 1010,  1  ; [14]


; Period value for each note
noteTable:  ; #c697
        dw 3822 ; [ 0] A♯₀
        dw 3608 ; [ 1] B₀
        dw 3405 ; [ 2] C₁
        dw 3214 ; [ 3] C♯₁
        dw 3034 ; [ 4] D₁
        dw 2863 ; [ 5] D♯₁
        dw 2703 ; [ 6] E₁
        dw 2551 ; [ 7] F₁
        dw 2408 ; [ 8] F♯₁
        dw 2273 ; [ 9] G₁
        dw 2145 ; [10] G♯₁
        dw 2025 ; [11] A₁
        dw 1911 ; [12] A♯₁
        dw 1804 ; [13] B₁
        dw 1703 ; [14] C₂
        dw 1607 ; [15] C♯₂
        dw 1517 ; [16] D₂
        dw 1432 ; [17] D♯₂
        dw 1351 ; [18] E₂
        dw 1276 ; [19] F₂
.err20: dw 1236 ; [20] F♯₂ (should be 1204)
        dw 1136 ; [21] G₂
        dw 1073 ; [22] G♯₂
        dw 1012 ; [23] A₂
.err24: dw  988 ; [24] A♯₂ (should be 956)
        dw  902 ; [25] B₂
        dw  851 ; [26] C₃
        dw  804 ; [27] C♯₃
        dw  758 ; [28] D₃
        dw  716 ; [29] D♯₃
        dw  676 ; [30] E₃
        dw  638 ; [31] F₃
        dw  602 ; [32] F♯₃
        dw  568 ; [33] G₃
        dw  536 ; [34] G♯₃
        dw  506 ; [35] A₃
        dw  478 ; [36] A♯₃
        dw  451 ; [37] B₃
        dw  426 ; [38] C₄
        dw  402 ; [39] C♯₄
        dw  379 ; [40] D₄
        dw  358 ; [41] D♯₄
        dw  338 ; [42] E₄
        dw  319 ; [43] F₄
        dw  301 ; [44] F♯₄
        dw  284 ; [45] G₄
        dw  268 ; [46] G♯₄
        dw  253 ; [47] A₄
        dw  239 ; [48] A♯₄
        dw  225 ; [49] B₄
        dw  213 ; [50] C₅
        dw  201 ; [51] C♯₅
        dw  190 ; [52] D₅
        dw  179 ; [53] D♯₅
        dw  169 ; [54] E₅
        dw  159 ; [55] F₅
        dw  150 ; [56] F♯₅
        dw  142 ; [57] G₅
        dw  134 ; [58] G♯₅
        dw  127 ; [59] A₅
        dw  119 ; [60] A♯₅
        dw  113 ; [61] B₅
        dw  106 ; [62] C₆
        dw  100 ; [63] C♯₆
        dw   95 ; [64] D₆
        dw   89 ; [65] D♯₆
        dw   84 ; [66] E₆
        dw   80 ; [67] F₆
        dw   75 ; [68] F♯₆
        dw   71 ; [69] G₆
        dw   67 ; [70] G♯₆
        dw   63 ; [71] A₆
        dw   60 ; [72] A♯₆
        dw   56 ; [73] B₆
        dw   53 ; [74] C₇
        dw   50 ; [75] C♯₇
        dw   47 ; [76] D₇
        dw   45 ; [77] D♯₇
        dw   42 ; [78] E₇
        dw   40 ; [79] F₇
        dw   38 ; [80] F♯₇
        dw   36 ; [81] G₇
        dw   34 ; [82] G♯₇
        dw   32 ; [83] A₇
.err84: dw   24 ; [84] A♯₇ (should be 30)


mixerValue:  ; #c741
        db mixOff

; #c742    mxT  mxN  mxM  period vol  susT   ?  vCnt vValue vSlope phAddr inAddr flag
ayChA:  Ch #FE, #F7, #09, #6F6D, #6E, #74, #79, #0D, #090A, #646C, #6320, #282C, #69
ayChB:  Ch #FD, #EF, #12, #7470, #78, #73, #69, #7A, #2965, #0A0D, #6C09, #2064, #62
ayChC:  Ch #FB, #DF, #24, #090A, #61, #64, #64, #20, #6C68, #622C, #0963, #6D3B, #6F

noisePeriod:  ; #c778
        db 0


; Play tone
;   `ix`: channel addr
;   `iy`: instrument addr
;   `hl`: period
;   `c`: duration
; Used by p_cb0c and p_cd25.
playTone:  ; #c779
        di
        ld a, iyl
        ld (ix+Ch.instrAddr+0), a
        ld a, iyh
        ld (ix+Ch.instrAddr+1), a

        ld (ix+Ch.period+0), l
        ld (ix+Ch.period+1), h
        ld (ix+Ch.duration), c

        ld a, (iy+Instr.e_5)
        ld (ix+Ch.ch_7), a

        ld a, (iy+Instr.vibrPeriod)
        and %01111111
        srl a
        jr NZ, .l_0
        ld a, 1
.l_0:
        ld (ix+Ch.vibrCounter), a

        ld a, (iy+Instr.vibrSlope+0)
        ld (ix+Ch.vibrSlope+0), a
        ld a, (iy+Instr.vibrSlope+1)
        ld (ix+Ch.vibrSlope+1), a

        xor a
        ld (ix+Ch.vibrValue+0), a
        ld (ix+Ch.vibrValue+1), a

        ld a, (mixerValue)
        or (ix+Ch.mixMask)
        ld c, (iy+Instr.flags)
        ld (ix+Ch.flags), c
        bit 0, c
        jr Z, .l_1
        and (ix+Ch.mixTone)
.l_1:
        bit 1, c
        jr Z, .l_2
        and (ix+Ch.mixNoise)
.l_2:
        ld (mixerValue), a
        bit 2, c
        jr NZ, .l_3

        ld hl, attackPhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret

.l_3:
        ; set envelope
        call doNothing

        ld a, (iy+Instr.attackSpd)  ; here, envelope shape
        ld c, regEnvShape
        call setAyReg

        ld a, (iy+Instr.attackLev)  ; here, envelope period
        ld c, regEnvPer
        call setAyReg
        inc c
        xor a
        call setAyReg

        ld (ix+Ch.volume), #FF

        jp justReturn


; Initialise AY registers and some struct fields
initAy:  ; #c7fe
        call doNothing

        ld c, regMixer
        ld a, (mixerValue)
        or mixOff
        ld (mixerValue), a
        call setAyReg
        xor a
        inc c                   ; regVolumeA
        call setAyReg
        inc c                   ; regVolumeB
        call setAyReg
        inc c                   ; regVolumeC
        call setAyReg
        ld a, 1
        inc c                   ; regEnvPer (low)
        call setAyReg
        inc c                   ; regEnvPer (high)
        xor a
        call setAyReg
        inc c                   ; regEnvShape
        call setAyReg

        ld (ayChA.flags), a
        ld (ayChB.flags), a
        ld (ayChC.flags), a
        ld (ayChA.volume), a
        ld (ayChB.volume), a
        ld (ayChC.volume), a

        jp justReturn


; Write values to AY registers
; Used by p_cb0c.
setAyValues:  ; #c83f
        ld a, (mixerValue)
        and mixOff
        cp mixOff
        ret Z

        ld ix, ayChA
        call applyEffect
        ld ix, ayChB
        call applyEffect
        ld ix, ayChC
        call applyEffect

        call doNothing
        ld ix, ayChA

        ; regMixer
        ld c, regMixer
        ld a, (mixerValue)
        call setAyReg

        ; regPeriodA
        ld c, regPeriodA
        ld a, (ayChA.period+0)
        add (ix+Ch.vibrValue+0)
        bit 1, (ix+Ch.flags)
        jp Z, .l_0
        ld (noisePeriod), a
.l_0:
        call setAyReg
        inc c
        ld a, (ayChA.period+1)
        adc a, (ix+Ch.vibrValue+1)
        call setAyReg

        ; regPeriodB
        inc c
        ld a, (ayChB.period+0)
        add (ix+Ch+Ch.vibrValue+0)
        bit 1, (ix+Ch+Ch.flags)
        jp Z, .l_1
        ld (noisePeriod), a
.l_1:
        call setAyReg
        inc c
        ld a, (ayChB.period+1)
        adc a, (ix+Ch+Ch.vibrValue+1)
        call setAyReg

        ; regPeriodC
        inc c
        ld a, (ayChC.period+0)
        add (ix+2*Ch+Ch.vibrValue+0)
        bit 1, (ix+2*Ch+Ch.flags)
        jp Z, .l_2
        ld (noisePeriod), a
.l_2:
        call setAyReg
        inc c
        ld a, (ayChC.period+1)
        adc a, (ix+2*Ch+Ch.vibrValue+1)
        call setAyReg

        ; regNoisePer
        inc c
        ld a, (noisePeriod)
   .3   rrca
        call setAyReg

        ; regVolumeA
        ld c, regVolumeA
        ld a, (ayChA.volume)
     .3 srl a
        call setAyReg

        ; regVolumeB
        inc c
        ld a, (ayChB.volume)
    .3  srl a
        call setAyReg

        ; regVolumeC
        inc c
        ld a, (ayChC.volume)
    .3  srl a
        call setAyReg

        jp justReturn


applyEffect:  ; #c8fb
        ld a, (mixerValue)
        and (ix+Ch.mixMask)
        cp (ix+Ch.mixMask)
        ret Z

        ld a, (ix+Ch.instrAddr+0)
        ld iyl, a
        ld a, (ix+Ch.instrAddr+1)
        ld iyh, a

        ld a, (ix+Ch.duration)
        and a
        jr Z, .l_0
        cp -1
        jr Z, .l_0
        dec (ix+Ch.duration)
.l_0:
        call p_c9a3

        bit 2, (iy+Instr.flags)
        jp NZ, p_c99c

        ld l, (ix+Ch.phaseAddr+0)
        ld h, (ix+Ch.phaseAddr+1)
        jp hl
        ; possible jumps:
        ; attackPhase, decayPhase, sustainPhase, releasePhase


attackPhase:  ; #c92d
        ld a, (ix+Ch.volume)
        add (iy+Instr.attackSpd)
        cp (iy+Instr.attackLev)
        jr NC, .end
        ld (ix+Ch.volume), a
        ret
.end:
        ld a, (iy+Instr.attackLev)
        ld (ix+Ch.volume), a
        ld hl, decayPhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret


decayPhase:  ; #c94c
        ld a, (ix+Ch.volume)
        add (iy+Instr.decaySpd)
        jp M, .end
        cp (iy+Instr.sustainLev)
        jr C, .end
        ld (ix+Ch.volume), a
        ret
.end:
        ld a, (iy+Instr.sustainLev)
        ld (ix+Ch.volume), a
        ld hl, sustainPhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret


sustainPhase:  ; #c96e
        ld a, (ix+Ch.duration)
        and a
        ret NZ

        ld hl, releasePhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret


releasePhase: ; #c967d
        ld a, (ix+Ch.volume)
        add (iy+Instr.releaseSpd)
        jp M, switchOff
        ld (ix+Ch.volume), a
        ret


switchOff:
        ld (ix+Ch.volume), 0
        ld a, (mixerValue)
        or (ix+Ch.mixMask)
        ld (mixerValue), a
        res 7, (ix+Ch.flags)
        ret


p_c99c:  ; #c99c
        ld a, (ix+Ch.duration)
        and a
        ret NZ
        jr switchOff


p_c9a3:  ; # c9a3
        ld a, (ix+Ch.ch_7)
        and a
        jr Z, .zero
        cp -1
        ret Z

        dec (ix+Ch.ch_7)
        ret NZ

.zero:
        ; update vibrato value
        ld l, (ix+Ch.vibrValue+0)
        ld h, (ix+Ch.vibrValue+1)
        ld c, (ix+Ch.vibrSlope+0)
        ld b, (ix+Ch.vibrSlope+1)
        add hl, bc
        ld (ix+Ch.vibrValue+0), l
        ld (ix+Ch.vibrValue+1), h

        dec (ix+Ch.vibrCounter)
        ret NZ

        ld a, (iy+Instr.vibrPeriod)
        and a
        ret Z

        jp P, .positive         ; always true?

        ld (ix+Ch.ch_7), -1     ; never happens?
        ret

.positive:
        ; reset vibrato counter with duration
        ld (ix+Ch.vibrCounter), a

        ; negate vibrato slope
        ld a, c
        cpl
        ld c, a
        ld a, b
        cpl
        ld b, a
        inc bc

        ld (ix+Ch.vibrSlope+0), c
        ld (ix+Ch.vibrSlope+1), b
        ret


; Why?
doNothing:  ; #c9e5
        ret

; Why?
justReturn:  ; #c9e6
        ret


; Writes value `a` to AY register `c`
; Used by p_c779.
setAyReg:  ; #c9e7
        push bc
        ld e, c
        ld bc, Port.ayRegister
        out (c), e
        ld b, high(Port.ayValue)
        out (c), a
        pop bc
        ret


; Unused?
callPlayMenuMusic:  ; #c9f4
        jp Code.callPlayMenuMusic

; Unused?
callAySoundFrame:  ; #c9f7
        jp Code.callAySoundFrame


; Non-zero if sound/music is playing, zero if silent
isPlaying:  ; #c9fa
        db 0

; (unused ? 50 bytes)
p_c9fb:  ; #c9fb
        dh C3 05 CA AF 32 FA C9 C3
        dh FE C7 F3 6F 5F 26 00 54
        dh 29 19 29 29 19 FD 21 D4
        dh C5 EB FD 19 FD 6E 0A FD
        dh 66 0B FD 4E 0C DD 21 42
        dh C7 CD 79 C7 DD CB 11 FE
        dh FB C9

; #ca2d
;              0      1      3      5      7    9   10   11   12   13   14   15     16   18   19   20
sectA:  Sect #00, #646F, #0D65, #090A, #646C, #20, #28, #69, #78, #2B, #73, #70, #6374, #6F, #6C, #29
sectB:  Sect #08, #3137, #0909, #733B, #7465, #20, #63, #6F, #6C, #6F, #75, #72, #7420, #6F, #20, #77
sectC:  Sect #10, #7469, #0D65, #090A, #6572, #74, #0D, #0A, #0D, #0A, #3B, #2D, #2D2D, #2D, #2D, #2D


; Table containing offsets in the `instruments` table pointing to instruments
; 3 × 8 bytes. Initialised at runtime as 0, 10, 20, 30, 40, 50, 60, 70
instrOffsets:  ; #ca6c
        dh 2D 2D 2D 2D 20 63 6F 69  ; junk values
        dh 6E 20 6A 75 6D 70 69 6E  ; junk values
        dh 67 20 2D 2D 2D 2D 2D 2D  ; junk values


; Initialises menu AY music
;   `a`: 0 - start, 1 - stop
playMenuMusic:  ; #ca84
        push af
        call initAy
        pop af

        ld l, a
        add a
        add l
        add a
        ; `a` *= 6
        ld hl, musicInitAddrs
        add l
        ld l, a
        jr NC, .l_0
        inc h
.l_0:
        ; `hl`: p_c4cb + 6 * `a`
        ld e, (hl)
        inc hl
        ld d, (hl)
        inc hl
        ld (sectA.i_1), de

        ld e, (hl)
        inc hl
        ld d, (hl)
        inc hl
        ld (sectB.i_1), de

        ld e, (hl)
        inc hl
        ld d, (hl)
        inc hl
        ld (sectC.i_1), de

        ; init with 0
        xor a
        ld (sectA.transpose), a
        ld (sectB.transpose), a
        ld (sectC.transpose), a
        ld (sectA.i_15), a
        ld (sectB.i_15), a
        ld (sectC.i_15), a

        ; init with -1
        cpl
        ld (sectA.i_11), a
        ld (sectB.i_11), a
        ld (sectC.i_11), a

        ; init with 1
        ld a, 1
        ld (sectA.i_9), a
        ld (sectB.i_9), a
        ld (sectC.i_9), a
        ld (sectA.duration), a
        ld (sectB.duration), a
        ld (sectC.duration), a

        ; init with (0, 10, 20, …, 70)
        ld hl, instrOffsets
        ld bc, #030A  ; `b`= 3, `c`= 10
.l_1:
        xor a
.l_2:
        ld (hl), a
        inc hl
        add c
        ld (hl), a
        inc hl
        add c
        cp 80
        jr NZ, .l_2
        djnz .l_1

        ; init with some addr(?)
        ld hl, instruments.i0
        ld (sectA.instrAddr), hl
        ld (sectB.instrAddr), hl
        ld (sectC.instrAddr), hl

        ; init with -1
        ld a, -1
        ld (sectA.isPlaying), a
        ld (sectB.isPlaying), a
        ld (sectC.isPlaying), a
        ld (isPlaying), a

        ret


; Called in interrupts
aySoundFrame:  ; #cb0c
        call setAyValues
        ld a, (isPlaying)
        and a
        ret Z

        ld a, (sectA.isPlaying)
        ld hl, sectB.isPlaying
        or (hl)
        ld hl, sectC.isPlaying
        or (hl)
        ld (isPlaying), a
        jr NZ, .l_0

        xor a
        ld (isPlaying), a
        ld a, (mixerValue)
        and mixOff
        cp mixOff
        ret Z

        ld a, 1
        ld (isPlaying), a
        ret

.l_0:
        ld iy, sectA
        ld ix, ayChA
        call .proccessChannel

        ld iy, sectB
        ld ix, ayChB
        call .proccessChannel

        ld iy, sectC
        ld ix, ayChC

.proccessChannel:
        call p_cc95

        ld a, (iy+Sect.i_11)
        and a
        jr Z, .l_10
.l_2:
        dec (iy+Sect.i_9)
        jr Z, .l_3

        ld a, (iy+Sect.scoreAddr+0)
        ld (iy+Sect.noteAddr+0), a
        ld a, (iy+Sect.scoreAddr+1)
        ld (iy+Sect.noteAddr+1), a
        ld (iy+Sect.i_11), 0
        jr .l_10

.l_3:
        ld (iy+Sect.i_9), 1
        ld l, (iy+Sect.i_1+0)
        ld h, (iy+Sect.i_1+1)
.l_4:
        ld a, (hl)
        cp #80
        jr C, .l_9

        cp #FE
        jr NZ, .l_5

        inc hl
        ld a, (hl)
        ld (iy+Sect.transpose), a
        inc hl
        jp .l_4

.l_5:
        cp #FF                  ; end marker
        jr NZ, .l_6

        ; end music
        xor a
        ld (iy+Sect.isPlaying), a
        ret

.l_6:
        cp #C0
        jr NC, .l_7

        and %00011111
        ld (iy+Sect.i_9), a
        inc hl
        jp .l_4

.l_7:
        and %00000111
        add (iy+Sect.i_0)
        ld de, instrOffsets
        add e
        ld e, a
        jr NC, .l_8
        inc d
.l_8:
        inc hl
        ldi                     ; copy val to 0..70 table
        jp .l_4

.l_9:
        ld (iy+Sect.i_11), 0
        inc hl
        ld (iy+Sect.i_1+0), l
        ld (iy+Sect.i_1+1), h
        ld c, a
        ld b, 0
        ld hl, scorePartAddrsLow
        add hl, bc
        ; `hl`: addr in p_c520 table (low)
        ld e, (hl)
        ld hl, scorePartAddrsHigh
        add hl, bc
        ; `hl`: addr in p_c53e table (high)
        ld d, (hl)
        ; `de`: addr in score?
        ld (iy+Sect.scoreAddr+0), e
        ld (iy+Sect.scoreAddr+1), d
        jr .l_11

.l_10:
        ld e, (iy+Sect.noteAddr+0)
        ld d, (iy+Sect.noteAddr+1)

.l_11:
        dec (iy+Sect.duration)
        jr Z, .zeroDuration

        ld a, (de)
        cp #80
        call NC, p_cc5b
        ld (iy+Sect.noteAddr+0), e
        ld (iy+Sect.noteAddr+1), d
        ret

.zeroDuration:
        ld a, (de)
        cp #80
        jr C, .l_13

        call p_cc5b
        ld a, (iy+Sect.i_11)
        and a
        jr Z, .zeroDuration
        jp .l_2

.l_13:
        cp #7F
        jr Z, .l_16             ; pause?

        cp #7E                  ; jump in score?
        jr NZ, .l_14

        inc de
        ld a, (de)
        ld l, a
        inc de
        ld a, (de)
        ld h, a
        jp .l_15

.l_14:
        add (iy+Sect.transpose)
        add 12                  ; octave
        ld (iy+Sect.note), a
        ld hl, noteTable
        add a
        ld c, a
        ld b, 0
        add hl, bc
        ; `hl` : addr in `noteTable`
        ld a, (hl)
        inc hl
        ld h, (hl)
        ld l, a
        ; `hl`: note period
.l_15:
        ld a, (iy+Sect.i_15)
        or %11000000
        ld (iy+Sect.i_20), a
        inc de
        ld a, (de)              ; duration
        inc de
        ld (iy+Sect.duration), a
        ld c, a
        ld (iy+Sect.noteAddr+0), e
        ld (iy+Sect.noteAddr+1), d

        ld e, (iy+Sect.instrAddr+0)
        ld a, (iy+Sect.instrAddr+1)
        ld iyh, a
        ld iyl, e
        ; `iy`: effect addr
        bit 7, (ix+Ch.flags)
        ret NZ

        jp playTone

.l_16:
        inc de
        ld a, (de)
        inc de
        ld (iy+Sect.duration), a
        ld (iy+Sect.noteAddr+0), e
        ld (iy+Sect.noteAddr+1), d
        ret


p_cc5b:  ; #cc5b
        ld a, (de)
        cp #88
        jr NC, .l_0

        and %00000111
        add (iy+Sect.i_0)
        ld c, a
        ld b, 0
        ld hl, instrOffsets
        add hl, bc
        ld c, (hl)
        ld hl, instruments
        add hl, bc
        ld (iy+Sect.instrAddr+0), l
        ld (iy+Sect.instrAddr+1), h
        inc de
        ret

.l_0:
        cp #FF
        jr NZ, .l_1
        ld (iy+Sect.i_11), #FF
        ret
.l_1:
        cp #C0
        jr NC, .l_2
        and #0F
        ld (iy+Sect.i_15), a
        inc de
        ret
.l_2:
        inc de
        cp #C2
        ret Z
        inc de
        inc de
        inc de
        ret


p_cc95:  ; #cc95
        bit 7, (ix+Ch.flags)
        ret NZ

        ld a, (iy+Sect.i_20)
        bit 7, a
        ret Z

        and %00111111
        jr NZ, .l_0
        res 7, (iy+Sect.i_20)
        ret

.l_0:
        ld d, %00000111
        bit 6, (iy+Sect.i_20)
        jr NZ, .l_2

        dec (iy+Sect.i_18)
        ret NZ

        dec (iy+Sect.i_19)
        jp Z, .l_2

        ld l, (iy+Sect.i_16+0)
        ld h, (iy+Sect.i_16+1)
        inc l
        ld (iy+Sect.i_16+0), l
        jp NZ, .l_1
        inc h
        ld (iy+Sect.i_16+1), h
.l_1:
        ld a, (hl)
        and d
        ld (iy+Sect.i_18), a
        ld a, (hl)
    .3  rrca
        and %00011111
        add (iy+Sect.note)
        jp .setPeriodByNote

.l_2:
        ld hl, p_c55c - #0708   ; `d`= 7 is added to `h`, `a`&%00011111: 1..5
        ld a, (iy+Sect.i_20)
    .3  add a
        ld e, a
        add hl, de              ; `hl`: #p_c55c + 8 * (Sect.i_20 - 1)

        bit 7, (hl)
        jr NZ, .l_3

        bit 6, (iy+Sect.i_20)
        jr NZ, .l_3

        ld (iy+Sect.i_19), 1
        ret

.l_3:
        res 6, (iy+Sect.i_20)
        ld a, (hl)
    .3  rrca
        and d
        ld (iy+Sect.i_18), a

        ld a, (hl)
        and d
        inc a
        ld (iy+Sect.i_19), a
        ld (iy+Sect.i_16+0), l
        ld (iy+Sect.i_16+1), h

        ld a, (iy+Sect.note)
.setPeriodByNote:
        add a
        ld hl, noteTable
        add l
        ld l, a
        jr NC, .noCarry
        inc h
.noCarry:
        ld a, (hl)
        ld (ix+Ch.period+0), a
        inc hl
        ld a, (hl)
        ld (ix+Ch.period+1), a
        ret


; Initialises an AY sound effect
;   `a`: sound index (0..14)
playAySound:  ; #cd25
        push hl, de, bc, ix, iy

        ld e, a
        ld l, a
        ld h, 0
    .2  add hl, hl
        ld c, l
        ld b, h
        add hl, hl
        add hl, bc
        ld c, a
        ld b, 0
        add hl, bc
        ; `hl`: a * 13
        ld bc, aySounds
        add hl, bc
        push hl : pop iy
        ; `iy`: sound effect addr

        ld a, (channelCounter)
        inc a
        cp 3
        jp C, .l_0
        xor a
.l_0:
        ld (channelCounter), a

        ld ix, ayChA
        and a
        jp Z, .l_1
        ld ix, ayChB
        cp 1
        jp Z, .l_1
        ld ix, ayChC
.l_1:
        ld l, (iy+Effect.period+0)
        ld h, (iy+Effect.period+1)
        ld c, (iy+Effect.duration)
        call playTone

        pop iy, ix, bc, de, hl
        ret


; (channel counter (0..2)?)
channelCounter:  ; #cd77
        db 0


    ENDMODULE
