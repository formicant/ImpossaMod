    MODULE AY


; Copied to RAM page 1 #C000
p_c000:  ; #c000
        dh 80 2C 30 2D 30 2C 30 31
        dh 30 2C 30 2D 30 2C 30 25
        dh 30 FF 7F 30 FF 80 91 1C
        dh 30 92 20 30 1F 30 22 30
        dh 20 30 93 23 30 22 30 94
        dh 27 30 90 FF 81 01 18 01
        dh 18 FF 81 01 18 01 18 01
        dh 18 01 18 01 18 01 18 01
        dh 0C 01 0C 01 0C 01 0C 01
        dh 18 01 18 01 18 01 18 01
        dh 18 01 18 01 0C 01 0C 01
        dh 0C 01 0C FF 82 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 2A 06 12 06 36
        dh 06 1E 06 12 06 12 06 12
        dh 06 12 0C 12 0C 12 06 12
        dh 06 FF 81 91 23 12 23 12
        dh 23 12 92 27 12 27 0C 27
        dh 0C 29 12 29 12 29 12 22
        dh 12 22 0C 22 0C 90 FF 81
        dh 0D 06 0D 06 0D 0C 0D 0C
        dh 0D 06 0D 0C 0D 0C 0D 06
        dh 08 0C 0B 0C FF 81 10 06
        dh 10 06 10 0C 10 0C 10 06
        dh 10 06 0B 06 0B 06 0B 0C
        dh 0B 0C 0B 06 0B 06 FF 81
        dh 0D 06 19 0C 0D 06 19 0C
        dh 0D 06 19 0C 0D 06 19 0C
        dh 0B 0C 08 0C FF 81 0D 06
        dh 0D 06 0D 0C 0D 12 0D 0C
        dh 0D 06 0D 0C 0D 0C 19 06
        dh 19 06 FF 81 10 06 10 06
        dh 10 0C 10 0C 10 06 10 06
        dh 0B 06 0B 06 0B 0C 0B 0C
        dh 0B 06 0B 06 FF 81 0D 06
        dh 0D 06 19 0C 0D 0C 19 06
        dh 0D 0C 0D 06 19 0C 0D 0C
        dh 19 0C 09 06 09 06 15 0C
        dh 09 0C 15 06 0B 0C 0B 06
        dh 17 0C 0B 0C 17 0C 0D 06
        dh 0D 06 19 0C 0D 0C 19 06
        dh 0D 0C 0D 06 19 0C 0D 0C
        dh 19 0C 10 06 10 06 1C 0C
        dh 10 0C 1C 06 0B 0C 0B 06
        dh 17 0C 0B 0C 17 0C FF 81
        dh 0D 06 0D 0C 0D 06 0D 0C
        dh 0D 06 0D 12 0B 0C 10 0C
        dh 0B 0C 09 06 09 0C 09 0C
        dh 09 0C 09 06 0B 0C 08 0C
        dh 0B 0C 0D 0C FF 83 2A 0C
        dh 2A 0C 1E 0C 2A 0C 2A 06
        dh 2A 0C 2A 06 1E 0C 1E 0C
        dh 2A 0C 2A 0C 1E 12 2A 0C
        dh 2A 06 2A 06 2A 06 1E 06
        dh 12 06 06 06 06 06 06 0C
        dh 06 0C 2A 0C 1E 0C 1E 0C
        dh 2A 0C 1E 0C 2A 0C 1E 0C
        dh 1E 0C 2A 0C 2A 06 2A 06
        dh 2A 06 36 06 36 06 2A 06
        dh 1E 06 12 06 06 06 06 06
        dh FF 84 19 0C 20 06 20 06
        dh 1F 0C 20 0C 25 0C 24 0C
        dh 25 0C 27 0C 19 0C 21 06
        dh 21 06 20 0C 21 0C 23 0C
        dh 21 0C 20 0C 1C 0C 19 0C
        dh 20 06 20 06 1F 0C 20 0C
        dh 25 0C 24 0C 25 0C 27 0C
        dh 28 0C 2A 06 2C 06 2D 0C
        dh 2C 06 2A 06 2C 0C 2A 0C
        dh 28 0C 27 0C FF 84 25 0C
        dh 20 0C 25 0C 2C 0C 28 18
        dh 27 18 25 0C 20 0C 25 0C
        dh 2C 0C 28 0C 27 0C 25 0C
        dh 23 0C 25 0C 20 0C 25 0C
        dh 2C 0C 28 18 27 0C 23 0C
        dh 21 30 20 30 FF 84 25 78
        dh 28 06 27 06 28 06 27 06
        dh 28 0C 2A 0C 28 0C 23 0C
        dh 25 78 28 06 27 06 28 06
        dh 27 06 28 0C 2A 0C 28 0C
        dh 29 0C FF 80 2A 78 2D 06
        dh 2C 06 2D 06 2C 06 2D 0C
        dh 2F 0C 2D 0C 28 0C 2A 60
        dh 85 2A 60 FF 84 14 0C 17
        dh 0C 16 0C 17 0C 14 0C 23
        dh 0C 22 0C 20 0C 1B 0C 1E
        dh 0C 1D 0C 1E 0C 1B 0C 2A
        dh 0C 29 0C 27 0C 1E 0C 21
        dh 0C 20 0C 21 0C 1E 0C 2D
        dh 0C 2C 0C 2A 0C 34 0C 33
        dh 0C 31 0C 28 0C 27 0C 25
        dh 0C 1C 0C 1B 0C FF 84 14
        dh 0C 17 0C 16 0C 17 0C 14
        dh 0C 23 0C 22 0C 20 0C 1B
        dh 0C 1E 0C 1D 0C 1E 0C 1B
        dh 0C 2A 0C 29 0C 27 0C 1E
        dh 0C 21 0C 20 0C 21 0C 1E
        dh 0C 2D 0C 2C 0C 2A 0C 22
        dh 0C 25 0C 24 0C 25 0C 22
        dh 0C 25 0C 28 0C 2B 0C FF
        dh 84 94 2C 0C 2C 18 2D 18
        dh 2C 18 2A 0C 2C 60 90 86
        dh 01 03 FF 7F 60 7F 60 7F
        dh 60 7F 60 FF 84 17 60 85
        dh 17 60 7F 60 7F 60 FF 82
        dh 12 60 12 60 12 60 12 06
        dh 12 06 12 06 12 06 12 06
        dh 12 06 12 06 12 06 12 06
        dh 12 06 12 06 12 06 12 06
        dh 12 06 12 06 12 06 FF 81
        dh 0D 0C 0D 0C 0D 0C 0D 0C
        dh 0D 0C 0D 0C 0D 0C 0D 0C
        dh 0D 0C 0D 0C 0D 0C 0D 0C
        dh 0D 0C 0D 06 0D 06 0D 0C
        dh 0D 06 0D 06 0D 0C 0D 0C
        dh 0D 0C 0D 0C 0D 0C 0D 0C
        dh 0D 0C 0D 0C 0D 06 0D 06
        dh 0D 0C 0D 0C 0D 06 0D 0C
        dh 0D 06 0D 06 0D 06 0D 0C
        dh 0D 06 0D 06 FF 81 14 06
        dh 14 06 14 0C 14 12 14 0C
        dh 14 06 14 0C 14 0C 14 06
        dh 14 06 0F 06 0F 06 0F 0C
        dh 0F 0C 0F 06 0F 0C 0F 0C
        dh 0F 06 0F 0C 0F 06 0F 06
        dh 12 06 12 06 12 0C 12 12
        dh 12 0C 12 06 12 0C 12 0C
        dh 12 06 12 06 0D 06 0D 06
        dh 0D 0C 0D 12 0D 0C 0D 06
        dh 0D 0C 0D 0C 0D 06 0D 06
        dh FF 81 14 06 14 06 14 0C
        dh 14 12 14 0C 14 06 14 0C
        dh 14 0C 14 06 14 06 0F 06
        dh 0F 06 0F 0C 0F 0C 0F 06
        dh 0F 0C 0F 0C 0F 06 0F 0C
        dh 0F 06 0F 06 12 06 12 06
        dh 12 0C 12 12 12 0C 12 06
        dh 12 0C 12 0C 12 06 12 06
        dh 16 06 0A 06 0A 0C 0A 12
        dh 0A 0C 0A 06 0A 0C 0A 0C
        dh 0A 06 0A 06 FF 81 08 0C
        dh 08 18 09 18 08 18 06 0C
        dh 08 60 FF

; used in playMenuMusic
p_c4cb: ;      A      B      C
.start: dw #C4D7, #C502, #C50A
.stop:  dw #C51E, #C51E, #C51E

p_c4d7:
        dh 88 03 04 1A 83 07
        dh 08 09 FE
        dh F9 09 FE 00 09 FE F9 09
        dh FE 00 83 0A 0B 0C 82 0D
        dh 0C 83 0A 0B 0C 82 0D 0C
        dh 0C FE 05 0C FE 00 1B 1C
        dh 1D FF 00 02 FE 05 02 8E
        dh 05 FF 16 16 19 17 82 06
        dh 0E 0F 10 16 0E 0F 10 10
        dh 11 12 13 14 15 FF 01 FF
p_c520:
        dh 00 12 15 2C 32 5C DA F7
        dh 0D 27 3D 53 6D BF E5 39
        dh 85 B5 DB F4 36 78 8B 94
        dh 9F 9F C7 15 69 BD
p_c53e:
        dh C0 C0
        dh C0 C0 C0 C0 C0 C0 C1 C1
        dh C1 C1 C1 C1 C1 C2 C2 C2
        dh C2 C2 C3 C3 C3 C3 C3 C3
        dh C3 C4 C4 C4 8A 21 49 00
        dh 00 00 00 00 8A 19 41 00
        dh 00 00 00 00 8A 29 49 00
        dh 00 00 00 00 8A 21 39 00
        dh 00 00 00 00 A1 31 00 00
        dh 00 00 00 00

; effect
p_c584:
        dh 60 FF 30 FF 30 10 06 01 00 01 7F FD 00

        dh FE 37 00 00 00 00 01
        dh 7F FA 00 FF 28 00 01 18
        dh 00 02 7F FC 00 FF 28 00
        dh 01 18 00 02 7F FE 32 FF
        dh 3E 20 03 02 00 01 7F FF
        dh 32 FF 40 00 00 01 00 01
        dh 7F FE 00 FF 00 20 03 02
        dh 00 01 60 FF 00 FF 30 10
        dh 06 01 00 01

; AY sound effects (15 × 13 bytes)
aySounds:  ; #c5d4
        ;                                 l  h  c
        dh 7F E9 01 FF 7F 00 00 A3 00 01 DA 05 01
        dh 1B FF 01 FF 50 00 00 01 00 01 2F 00 01
        dh 7F E9 01 FF 7F 00 00 29 FF 01 4E 03 01
        dh 08 F2 01 F9 6B FF 00 00 00 05 3C 0F 02
        dh 0E F2 01 F9 29 FF 00 00 00 02 58 00 01
        dh 7F FC 01 FF 44 00 00 EC FF 01 6A 08 01
        dh 7F FD 01 FF 60 00 00 00 FF 02 F0 00 01
        dh 0C 00 00 00 71 00 00 9C 00 05 BB 0F 0F
        dh 0A FA 01 F6 00 00 00 FF FF 05 23 00 1C
        dh 14 EC 01 FF 7F 01 00 00 00 02 7C 00 0A
        dh 7F FD 01 FF 7F 04 00 E0 FF 01 6D 09 01
        dh 7F FA 01 FF 7F 00 00 F2 FF 01 7D 02 01
        dh 7F F6 01 FF 7F 00 00 00 00 01 84 0D 01
        dh 7F F9 01 FF 7F 00 00 00 00 01 C8 00 01
        dh 7F FD 08 DD 7E 00 00 DD FC 01 F2 03 01


; Note table
p_c697:  ; #c697
        dw #0EEE
        dw #0E18
        dw #0D4D
        dw #0C8E
        dw #0BDA
        dw #0B2F
        dw #0A8F
        dw #09F7
        dw #0968
        dw #08E1
        dw #0861
        dw #07E9
        dw #0777
        dw #070C
        dw #06A7
        dw #0647
        dw #05ED
        dw #0598
        dw #0547
        dw #04FC
        dw #04D4
        dw #0470
        dw #0431
        dw #03F4
        dw #03DC
        dw #0386
        dw #0353
        dw #0324
        dw #02F6
        dw #02CC
        dw #02A4
        dw #027E
        dw #025A
        dw #0238
        dw #0218
        dw #01FA
        dw #01DE
        dw #01C3
        dw #01AA
        dw #0192
        dw #017B
        dw #0166
        dw #0152
        dw #013F
        dw #012D
        dw #011C
        dw #010C
        dw #00FD
        dw #00EF
        dw #00E1
        dw #00D5
        dw #00C9
        dw #00BE
        dw #00B3
        dw #00A9
        dw #009F
        dw #0096
        dw #008E
        dw #0086
        dw #007F
        dw #0077
        dw #0071
        dw #006A
        dw #0064
        dw #005F
        dw #0059
        dw #0054
        dw #0050
        dw #004B
        dw #0047
        dw #0043
        dw #003F
        dw #003C
        dw #0038
        dw #0035
        dw #0032
        dw #002F
        dw #002D
        dw #002A
        dw #0028
        dw #0026
        dw #0024
        dw #0022
        dw #0020
        dw #0018


mixerValue:  ; #c741
        db mixOff

; #c742    mxT  mxN  mxM  period vol  susT   ?  vCnt vValue vSlope phAddr inAddr flag
ayChA:  Ch #FE, #F7, #09, #6F6D, #6E, #74, #79, #0D, #090A, #646C, #6320, #282C, #69
ayChB:  Ch #FD, #EF, #12, #7470, #78, #73, #69, #7A, #2965, #0A0D, #6C09, #2064, #62
ayChC:  Ch #FB, #DF, #24, #090A, #61, #64, #64, #20, #6C68, #622C, #0963, #6D3B, #6F

noisePeriod:  ; #c778
        db 0


; Play sound effect
;   `ix`: channel addr
;   `iy`: effect addr
;   `hl`: period
;   `c`: duration
; Used by p_cb0c and p_cd25.
playEffect:  ; #c779
        di
        ld a, iyl
        ld (ix+Ch.effectAddr+0), a
        ld a, iyh
        ld (ix+Ch.effectAddr+1), a

        ld (ix+Ch.period+0), l
        ld (ix+Ch.period+1), h
        ld (ix+Ch.duration), c

        ld a, (iy+Effect.e_5)
        ld (ix+Ch.ch_7), a

        ld a, (iy+Effect.vibrPeriod)
        and %01111111
        srl a
        jr NZ, .l_0
        ld a, 1
.l_0:
        ld (ix+Ch.vibrCounter), a

        ld a, (iy+Effect.vibrSlope+0)
        ld (ix+Ch.vibrSlope+0), a
        ld a, (iy+Effect.vibrSlope+1)
        ld (ix+Ch.vibrSlope+1), a

        xor a
        ld (ix+Ch.vibrValue+0), a
        ld (ix+Ch.vibrValue+1), a

        ld a, (mixerValue)
        or (ix+Ch.mixMask)
        ld c, (iy+Effect.flags)
        ld (ix+Ch.flags), c
        bit 0, c
        jr Z, .l_1
        and (ix+Ch.mixTone)
.l_1:
        bit 1, c
        jr Z, .l_2
        and (ix+Ch.mixNoise)
.l_2:
        ld (mixerValue), a
        bit 2, c
        jr NZ, .l_3

        ld hl, onsetPhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret

.l_3:
        ; set envelope
        call doNothing

        ld a, (iy+Effect.envShape)
        ld c, regEnvShape
        call setAyReg

        ld a, (iy+Effect.envPeriod)
        ld c, regEnvPer
        call setAyReg
        inc c
        xor a
        call setAyReg

        ld (ix+Ch.volume), #FF

        jp justReturn


; Initialize AY registers and some struct fields
initAy:  ; #c7fe
        call doNothing

        ld c, regMixer
        ld a, (mixerValue)
        or mixOff
        ld (mixerValue), a
        call setAyReg
        xor a
        inc c                   ; regVolumeA
        call setAyReg
        inc c                   ; regVolumeB
        call setAyReg
        inc c                   ; regVolumeC
        call setAyReg
        ld a, 1
        inc c                   ; regEnvPer (low)
        call setAyReg
        inc c                   ; regEnvPer (high)
        xor a
        call setAyReg
        inc c                   ; regEnvShape
        call setAyReg

        ld (ayChA.flags), a
        ld (ayChB.flags), a
        ld (ayChC.flags), a
        ld (ayChA.volume), a
        ld (ayChB.volume), a
        ld (ayChC.volume), a

        jp justReturn


; Write values to AY registers
; Used by p_cb0c.
setAyValues:  ; #c83f
        ld a, (mixerValue)
        and mixOff
        cp mixOff
        ret Z

        ld ix, ayChA
        call applyEffect
        ld ix, ayChB
        call applyEffect
        ld ix, ayChC
        call applyEffect

        call doNothing
        ld ix, ayChA

        ; regMixer
        ld c, regMixer
        ld a, (mixerValue)
        call setAyReg

        ; regPeriodA
        ld c, regPeriodA
        ld a, (ayChA.period+0)
        add (ix+Ch.vibrValue+0)
        bit 1, (ix+Ch.flags)
        jp Z, .l_0
        ld (noisePeriod), a
.l_0:
        call setAyReg
        inc c
        ld a, (ayChA.period+1)
        adc a, (ix+Ch.vibrValue+1)
        call setAyReg

        ; regPeriodB
        inc c
        ld a, (ayChB.period+0)
        add (ix+Ch+Ch.vibrValue+0)
        bit 1, (ix+Ch+Ch.flags)
        jp Z, .l_1
        ld (noisePeriod), a
.l_1:
        call setAyReg
        inc c
        ld a, (ayChB.period+1)
        adc a, (ix+Ch+Ch.vibrValue+1)
        call setAyReg

        ; regPeriodC
        inc c
        ld a, (ayChC.period+0)
        add (ix+2*Ch+Ch.vibrValue+0)
        bit 1, (ix+2*Ch+Ch.flags)
        jp Z, .l_2
        ld (noisePeriod), a
.l_2:
        call setAyReg
        inc c
        ld a, (ayChC.period+1)
        adc a, (ix+2*Ch+Ch.vibrValue+1)
        call setAyReg

        ; regNoisePer
        inc c
        ld a, (noisePeriod)
   .3   rrca
        call setAyReg

        ; regVolumeA
        ld c, regVolumeA
        ld a, (ayChA.volume)
     .3 srl a
        call setAyReg

        ; regVolumeB
        inc c
        ld a, (ayChB.volume)
    .3  srl a
        call setAyReg

        ; regVolumeC
        inc c
        ld a, (ayChC.volume)
    .3  srl a
        call setAyReg

        jp justReturn


applyEffect:  ; #c8fb
        ld a, (mixerValue)
        and (ix+Ch.mixMask)
        cp (ix+Ch.mixMask)
        ret Z

        ld a, (ix+Ch.effectAddr+0)
        ld iyl, a
        ld a, (ix+Ch.effectAddr+1)
        ld iyh, a

        ld a, (ix+Ch.duration)
        and a
        jr Z, .l_0
        cp -1
        jr Z, .l_0
        dec (ix+Ch.duration)
.l_0:
        call p_c9a3

        bit 2, (iy+Effect.flags)
        jp NZ, p_c99c

        ld l, (ix+Ch.phaseAddr+0)
        ld h, (ix+Ch.phaseAddr+1)
        jp hl
        ; possible jumps:
        ; onsetPhase, decayPhase, sustainPhase, releasePhase


onsetPhase:  ; #c92d
        ld a, (ix+Ch.volume)
        add (iy+Effect.envShape)
        cp (iy+Effect.envPeriod)
        jr NC, .end
        ld (ix+Ch.volume), a
        ret
.end:
        ld a, (iy+Effect.envPeriod)
        ld (ix+Ch.volume), a
        ld hl, decayPhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret


decayPhase:  ; #c94c
        ld a, (ix+Ch.volume)
        add (iy+Effect.decaySpd)
        jp M, .end
        cp (iy+Effect.sustainLev)
        jr C, .end
        ld (ix+Ch.volume), a
        ret
.end:
        ld a, (iy+Effect.sustainLev)
        ld (ix+Ch.volume), a
        ld hl, sustainPhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret


sustainPhase:  ; #c96e
        ld a, (ix+Ch.duration)
        and a
        ret NZ

        ld hl, releasePhase
        ld (ix+Ch.phaseAddr+0), l
        ld (ix+Ch.phaseAddr+1), h
        ret


releasePhase: ; #c967d
        ld a, (ix+Ch.volume)
        add (iy+Effect.releaseSpd)
        jp M, switchOff
        ld (ix+Ch.volume), a
        ret


switchOff:
        ld (ix+Ch.volume), 0
        ld a, (mixerValue)
        or (ix+Ch.mixMask)
        ld (mixerValue), a
        res 7, (ix+Ch.flags)
        ret


p_c99c:  ; #c99c
        ld a, (ix+Ch.duration)
        and a
        ret NZ
        jr switchOff


p_c9a3:  ; # c9a3
        ld a, (ix+Ch.ch_7)
        and a
        jr Z, .zero
        cp -1
        ret Z

        dec (ix+Ch.ch_7)
        ret NZ

.zero:
        ; update vibrato value
        ld l, (ix+Ch.vibrValue+0)
        ld h, (ix+Ch.vibrValue+1)
        ld c, (ix+Ch.vibrSlope+0)
        ld b, (ix+Ch.vibrSlope+1)
        add hl, bc
        ld (ix+Ch.vibrValue+0), l
        ld (ix+Ch.vibrValue+1), h

        dec (ix+Ch.vibrCounter)
        ret NZ

        ld a, (iy+Effect.vibrPeriod)
        and a
        ret Z

        jp P, .positive
        ld (ix+Ch.ch_7), -1
        ret

.positive:
        ; reset vibrato counter with duration
        ld (ix+Ch.vibrCounter), a

        ; negate vibrato slope
        ld a, c
        cpl
        ld c, a
        ld a, b
        cpl
        ld b, a
        inc bc

        ld (ix+Ch.vibrSlope+0), c
        ld (ix+Ch.vibrSlope+1), b
        ret


; Why?
doNothing:  ; #c9e5
        ret

; Why?
justReturn:  ; #c9e6
        ret


; Writes value `a` to AY register `c`
; Used by p_c779.
setAyReg:  ; #c9e7
        push bc
        ld e, c
        ld bc, registPort
        out (c), e
        ld b, high(valuePort)
        out (c), a
        pop bc
        ret


; Unused?
callPlayMenuMusic:  ; #c9f4
        jp Code.callPlayMenuMusic

; Unused?
callAySoundFrame:  ; #c9f7
        jp Code.callAySoundFrame


; Non-zero if sound/music is playing, zero if silent
isPlaying:  ; #c9fa
        db 0

; (unused ?)
p_c9fb:  ; #c9fb
        dh C3 05 CA AF 32 FA C9 C3
        dh FE C7 F3 6F 5F 26 00 54
        dh 29 19 29 29 19 FD 21 D4
        dh C5 EB FD 19 FD 6E 0A FD
        dh 66 0B FD 4E 0C DD 21 42
        dh C7 CD 79 C7 DD CB 11 FE
        dh FB C9

; #ca2d
partA:  Part #00, #646F, #0D65, #090A, #646C, #20, #28, #69, #78, #2B, #73, #70, #6374, #6F, #6C, #29
partB:  Part #08, #3137, #0909, #733B, #7465, #20, #63, #6F, #6C, #6F, #75, #72, #7420, #6F, #20, #77
partC:  Part #10, #7469, #0D65, #090A, #6572, #74, #0D, #0A, #0D, #0A, #3B, #2D, #2D2D, #2D, #2D, #2D

; 3 × 8 bytes
; Initialized as 0, 10, 20, 30, 40, 50, 60, 70
p_ca6c:  ; #ca6c
        dh 2D 2D 2D 2D 20 63 6F 69
        dh 6E 20 6A 75 6D 70 69 6E
        dh 67 20 2D 2D 2D 2D 2D 2D


; Initializes menu AY music
;   `a`: 0 - start, 1 - stop
playMenuMusic:  ; #ca84
        push af
        call initAy
        pop af

        ld l, a
        add a
        add l
        add a
        ; `a` *= 6
        ld hl, p_c4cb
        add l
        ld l, a
        jr NC, .l_0
        inc h
.l_0:
        ; `hl`: p_c4cb + 6 * `a`
        ld e, (hl)
        inc hl
        ld d, (hl)
        inc hl
        ld (partA.i_1), de

        ld e, (hl)
        inc hl
        ld d, (hl)
        inc hl
        ld (partB.i_1), de

        ld e, (hl)
        inc hl
        ld d, (hl)
        inc hl
        ld (partC.i_1), de

        ; init with 0
        xor a
        ld (partA.i_10), a
        ld (partB.i_10), a
        ld (partC.i_10), a
        ld (partA.i_15), a
        ld (partB.i_15), a
        ld (partC.i_15), a

        ; init with -1
        cpl
        ld (partA.i_11), a
        ld (partB.i_11), a
        ld (partC.i_11), a

        ; init with 1
        ld a, 1
        ld (partA.i_9), a
        ld (partB.i_9), a
        ld (partC.i_9), a
        ld (partA.i_12), a
        ld (partB.i_12), a
        ld (partC.i_12), a

        ; init with (0, 10, 20, …, 70)
        ld hl, p_ca6c
        ld bc, #030A  ; `b`= 3, `c`= 10
.l_1:
        xor a
.l_2:
        ld (hl), a
        inc hl
        add c
        ld (hl), a
        inc hl
        add c
        cp 80
        jr NZ, .l_2
        djnz .l_1

        ; init with some addr(?)
        ld hl, p_c584
        ld (partA.effectAddr), hl
        ld (partB.effectAddr), hl
        ld (partC.effectAddr), hl

        ; init with -1
        ld a, -1
        ld (partA.i_13), a
        ld (partB.i_13), a
        ld (partC.i_13), a
        ld (isPlaying), a

        ret


; Called in interrupts
aySoundFrame:  ; #cb0c
        call setAyValues
        ld a, (isPlaying)
        and a
        ret Z

        ld a, (partA.i_13)
        ld hl, partB.i_13
        or (hl)
        ld hl, partC.i_13
        or (hl)
        ld (isPlaying), a
        jr NZ, .l_0

        xor a
        ld (isPlaying), a
        ld a, (mixerValue)
        and mixOff
        cp mixOff
        ret Z

        ld a, 1
        ld (isPlaying), a
        ret

.l_0:
        ld iy, partA
        ld ix, ayChA
        call .l_1

        ld iy, partB
        ld ix, ayChB
        call .l_1

        ld iy, partC
        ld ix, ayChC
.l_1:
        call p_cc95
        ld a, (iy+Part.i_11)
        and a
        jr Z, .l_10
.l_2:
        dec (iy+Part.i_9)
        jr Z, .l_3
        ld a, (iy+Part.i_3+0)
        ld (iy+Part.i_5+0), a
        ld a, (iy+Part.i_3+1)
        ld (iy+Part.i_5+1), a
        ld (iy+Part.i_11), 0
        jr .l_10

.l_3:
        ld (iy+Part.i_9), 1
        ld l, (iy+Part.i_1+0)
        ld h, (iy+Part.i_1+1)
.l_4:
        ld a, (hl)
        cp #80
        jr C, .l_9
        cp #FE
        jr NZ, .l_5
        inc hl
        ld a, (hl)
        ld (iy+Part.i_10), a
        inc hl
        jp .l_4
.l_5:
        cp #FF
        jr NZ, .l_6
        xor a
        ld (iy+Part.i_13), a
        ret
.l_6:
        cp #C0
        jr NC, .l_7
        and #1F
        ld (iy+Part.i_9), a
        inc hl
        jp .l_4
.l_7:
        and #07
        add (iy+Part.i_0)
        ld de, p_ca6c
        add e
        ld e, a
        jr NC, .l_8
        inc d
.l_8:
        inc hl
        ldi
        jp .l_4
.l_9:
        ld (iy+Part.i_11), #00
        inc hl
        ld (iy+Part.i_1+0), l
        ld (iy+Part.i_1+1), h
        ld c, a
        ld b, #00
        ld hl, p_c520
        add hl, bc
        ld e, (hl)
        ld hl, p_c53e
        add hl, bc
        ld d, (hl)
        ld (iy+Part.i_3+0), e
        ld (iy+Part.i_3+1), d
        jr .l_11
.l_10:
        ld e, (iy+Part.i_5+0)
        ld d, (iy+Part.i_5+1)
.l_11:
        dec (iy+Part.i_12)
        jr Z, .l_12
        ld a, (de)
        cp #80
        call NC, p_cc5b
        ld (iy+Part.i_5+0), e
        ld (iy+Part.i_5+1), d
        ret
.l_12:
        ld a, (de)
        cp #80
        jr C, .l_13
        call p_cc5b
        ld a, (iy+Part.i_11)
        and a
        jr Z, .l_12
        jp .l_2
.l_13:
        cp #7F
        jr Z, .l_16
        cp #7E
        jr NZ, .l_14
        inc de
        ld a, (de)
        ld l, a
        inc de
        ld a, (de)
        ld h, a
        jp .l_15
.l_14:
        add (iy+Part.i_10)
        add #0C
        ld (iy+Part.i_14), a
        ld hl, p_c697
        add a
        ld c, a
        ld b, #00
        add hl, bc
        ld a, (hl)
        inc hl
        ld h, (hl)
        ld l, a
.l_15:
        ld a, (iy+Part.i_15)
        or #C0
        ld (iy+Part.i_20), a
        inc de
        ld a, (de)
        inc de
        ld (iy+Part.i_12), a
        ld c, a
        ld (iy+Part.i_5+0), e
        ld (iy+Part.i_5+1), d
        ld e, (iy+Part.effectAddr+0)
        ld a, (iy+Part.effectAddr+1)
        ld iyh, a
        ld iyl, e
        bit 7, (ix+Ch.flags)
        ret NZ
        jp playEffect

.l_16:
        inc de
        ld a, (de)
        inc de
        ld (iy+Part.i_12), a
        ld (iy+Part.i_5+0), e
        ld (iy+Part.i_5+1), d
        ret


p_cc5b:  ; #cc5b
        ld a, (de)
        cp #88
        jr NC, .l_0
        and #07
        add (iy+Part.i_0)
        ld c, a
        ld b, #00
        ld hl, p_ca6c
        add hl, bc
        ld c, (hl)
        ld hl, p_c584
        add hl, bc
        ld (iy+Part.effectAddr+0), l
        ld (iy+Part.effectAddr+1), h
        inc de
        ret
.l_0:
        cp #FF
        jr NZ, .l_1
        ld (iy+Part.i_11), #FF
        ret
.l_1:
        cp #C0
        jr NC, .l_2
        and #0F
        ld (iy+Part.i_15), a
        inc de
        ret
.l_2:
        inc de
        cp #C2
        ret Z
        inc de
        inc de
        inc de
        ret


p_cc95:  ; #cc95
        bit 7, (ix+Ch.flags)
        ret NZ

        ld a, (iy+Part.i_20)
        bit 7, a
        ret Z

        and mixOff
        jr NZ, .l_0
        res 7, (iy+Part.i_20)
        ret

.l_0:
        ld d, %00000111
        bit 6, (iy+Part.i_20)
        jr NZ, .l_2
        dec (iy+Part.i_18)
        ret NZ

        dec (iy+Part.i_19)
        jp Z, .l_2

        ld l, (iy+Part.i_16+0)
        ld h, (iy+Part.i_16+1)
        inc l
        ld (iy+Part.i_16+0), l
        jp NZ, .l_1
        inc h
        ld (iy+Part.i_16+1), h
.l_1:
        ld a, (hl)
        and d
        ld (iy+Part.i_18), a
        ld a, (hl)
    .3  rrca
        and %00011111
        add (iy+Part.i_14)
        jp .l_4

.l_2:
        ld hl, #BE54            ; #C554 - #0700
        ld a, (iy+Part.i_20)
    .3  add a
        ld e, a
        add hl, de              ; `hl`: #C554 + 8 * Part.i_20

        bit 7, (hl)
        jr NZ, .l_3

        bit 6, (iy+Part.i_20)
        jr NZ, .l_3

        ld (iy+Part.i_19), 1
        ret

.l_3:
        res 6, (iy+Part.i_20)
        ld a, (hl)
    .3  rrca
        and d
        ld (iy+Part.i_18), a

        ld a, (hl)
        and d
        inc a
        ld (iy+Part.i_19), a
        ld (iy+Part.i_16+0), l
        ld (iy+Part.i_16+1), h

        ld a, (iy+Part.i_14)
.l_4:
        add a
        ld hl, p_c697
        add l
        ld l, a
        jr NC, .l_5
        inc h
.l_5:
        ld a, (hl)
        ld (ix+Ch.period+0), a
        inc hl
        ld a, (hl)
        ld (ix+Ch.period+1), a
        ret


; Initializes an AY sound effect
;   `a`: sound index (0..14)
playAySound:  ; #cd25
        push hl, de, bc, ix, iy

        ld e, a
        ld l, a
        ld h, 0
    .2  add hl, hl
        ld c, l
        ld b, h
        add hl, hl
        add hl, bc
        ld c, a
        ld b, 0
        add hl, bc
        ; `hl`: a * 13
        ld bc, aySounds
        add hl, bc
        push hl
        pop iy
        ; `iy`: sound effect addr

        ld a, (channelCounter)
        inc a
        cp 3
        jp C, .l_0
        xor a
.l_0:
        ld (channelCounter), a

        ld ix, ayChA
        and a
        jp Z, .l_1
        ld ix, ayChB
        cp 1
        jp Z, .l_1
        ld ix, ayChC
.l_1:
        ld l, (iy+Effect.period+0)
        ld h, (iy+Effect.period+1)
        ld c, (iy+Effect.duration)
        call playEffect

        pop iy, ix, bc, de, hl
        ret


; (channel counter (0..2)?)
channelCounter:  ; #cd77
        db 0


    ENDMODULE
